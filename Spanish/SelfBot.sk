# ###############################################################################
# Ejemplo de Discord SelfBot en Skript por Blueyescat
#
# Incluye comandos buenos/Ãºtiles (con multiples prefijos) y un sistema de evaluaciÃ³n.
#
# Requiere los addons de Skript: Vixio, TuSKe y skQuery
# ###############################################################################

options:
	#Bot Token
	#
	#Token de tu cuenta de Discord. 
	#Puedes obtener el token presionando CTRL+SHIFT+I en Discord y siguiendo estos pasos: http://i.imgur.com/kyaHOz8.png
	#Â¡No le des el token de tÃº cuenta de Discord a nadie!
	token: ""
	
	#No le tomes importancia, es el nombre de referencia en el cÃ³digo (No es el nombre de usuario en Discord)
	botName: "SelfBot"

	#
	#
  #SÃ­ quieres usar emojis como prefijo en los comandos, obtÃ©n el cÃ¡racter unicode de este en: https://emojipedia.org
  #Por ejemplo, :apple: en Discord: "ðŸŽ"
  #
  #SÃ­ quieres usar la etiqueta de menciÃ³n como prefijo de comando puedes usar directamente "@nombreDeUsuario"
	prefixes: "> " and ">" #"> " (Sin el espacio) deberÃ­a estar antes de ">"

#Intenta iniciar sesiÃ³n a la cuenta del Discord bot cuando Skript es activado
#Â¡Requiere reiniciar el servidor!
on skript load:
	login to user with token {@token} with name {@botName}

on guild message sent by {@botName}:
	#
	#Formato del comando
	#
	loop {@prefixes}:
		if first (length of "%loop-value%") characters of event-string is loop-value:
			set {_prefix} to loop-value
			set {_strings::*} to split regex replace all "^\s+" with "" in regex replace all "(.*?)\n```" with "$1 ```" in (subtext of event-string from character length of {_prefix} + 1 to length of event-string) at " "
			set {_command} to {_strings::1}
			delete {_strings::1}
			set {_arg::*} to {_strings::*}
			delete {_strings::*}
			set {_arguments} to join {_arg::*} with " "
			stop loop
	#
	#Variables usadas en el sistema de comandos:
	# {_prefix} - {_command} - {_arguments} - {_arg::<number>}
	#
	if {_command} is set:
		if {_command} is "eval" or "evaluate":
			if {_arguments} is set:
				#
				#Remueve los espacios en el principio de los argumentos
				#
				set {_arguments} to regex replace "^\s+" with "" in {_arguments}
				#
				#Obtiene el cÃ³digo de Skript desde un bloque de cÃ³digo (No aÃ±adÃ­ soporte para bloques de cÃ³digo de una lÃ­nea)
				#
				if first 3 characters of {_arguments} is "```":
					if last 3 characters of {_arguments} is "```":
						if first 5 characters of {_arguments} is "```vb": #Soporte de resalto de sintaxis de Visual Basic en bloques de cÃ³digo
							set {_arguments} to subtext of {_arguments} from character 7 to (length of {_arguments} - 3)
						else:
							set {_arguments} to subtext of {_arguments} from character 4 to (length of {_arguments} - 3)
					else:
						create embed "evaluate"
						set title of embed "evaluate" to ":x: Â¡Bloque de cÃ³digo invÃ¡lido!"
						set color of embed "evaluate" to "RED"
						send embed "evaluate" to channel event-channel with {@botName}
						delete embed "evaluate"
						stop
				#
				#EvalÃºa el cÃ³digo de Skript y obtiene los errores sÃ­ hay
				#
				evaluate logging in {_errors::*}: {_arguments}
				if {_errors::*} is set:
					#
					#Cambiar el formato de los errores
					#
					loop {_errors::*}:
						set {_parse::*} to loop-value parsed as "%text% \(TuSKe\/evaluate.sk, line %integer%\: %text%'\)"
						add "**LÃ­nea %{_parse::2}%:** *%{_parse::1}%*%nl%**>** ``%{_parse::3}%``" to {_lines::*}
					create embed "evaluate"
					set title of embed "evaluate" to ":x: Errores de evaluaciÃ³n (%size of {_errors::*}%)"
					set description of embed "evaluate" to join {_lines::*} with nl
					set color of embed "evaluate" to "RED"
					send embed "evaluate" to channel event-channel with {@botName}
					delete embed "evaluate"
		else if {_command} is "clear" or "purge":
			if {_arguments} parsed as number is set:
				clear {_arguments} parsed as number messages in event-channel with {@botName}
			else:
				reply with ":exclamation: **Uso del comando: %{_prefix}%**%{_command}% <NÃºmero entre 1 y 100>"
