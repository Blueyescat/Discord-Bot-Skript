# ###############################################################################
# Ejemplo de Discord SelfBot en Skript por Blueyescat
#
# Incluye comandos buenos/煤tiles (con multiples prefijos) y un sistema de evaluaci贸n.
#
# Requiere los addons de Skript: Vixio, TuSKe y skQuery
# ###############################################################################

options:
	#Bot Token
	#
	#Token de tu cuenta de Discord. 
	#Puedes obtener el token presionando CTRL+SHIFT+I en Discord y siguiendo estos pasos: http://i.imgur.com/kyaHOz8.png
	#隆No le des el token de t煤 cuenta de Discord a nadie!
	token: ""
	
	#No le tomes importancia, es el nombre de referencia en el c贸digo (No es el nombre de usuario en Discord)
	botName: "SelfBot"

	#
	#
  #S铆 quieres usar emojis como prefijo en los comandos, obt茅n el c谩racter unicode de este en: https://emojipedia.org
  #Por ejemplo, :apple: en Discord: ""
  #
  #S铆 quieres usar la etiqueta de menci贸n como prefijo de comando puedes usar directamente "@nombreDeUsuario"
	prefixes: "> " and ">" # "> " (with space) must come before ">" (without space)

#Intenta iniciar sesi贸n a la cuenta del Discord bot cuando Skript es activado
#隆Requiere reiniciar el servidor!
on skript load:
	login to user with token {@token} with name {@botName}

on guild message sent by {@botName}:
	#
	#Formato del comando
	#
	loop {@prefixes}:
		if event-string starts with "%loop-value%":
			set {_prefix} to loop-value
			set {_strings::*} to split regex replace all "(.*?)\n```" with "$1 ```" in event-string at " "
			if {_prefix} contains " ":
				set {_command} to {_strings::2}
				delete {_strings::1} and {_strings::2}
			else:
				set {_command} to subtext of {_strings::1} from character (length of {_prefix}) + 1 to length of {_strings::1}
				delete {_strings::1}
			set {_arg::*} to {_strings::*}
			set {_arguments} to join {_arg::*} with " "
			stop loop
	#
	#Variables usadas en el sistema de comandos:
	# {_prefix} - {_command} - {_arguments} - {_arg::<number>}
	#
	if {_command} is set:
		if {_command} is "eval" or "evaluate":
			if {_arguments} is set:
				#
				#Remueve los espacios en el principio de los argumentos
				#
				set {_arguments} to regex replace "^\s+" with "" in {_arguments}
				#
				#Obtiene el c贸digo de Skript desde un bloque de c贸digo (No a帽ad铆 soporte para bloques de c贸digo de una l铆nea)
				#
				if first 3 characters of {_arguments} is "```":
					if last 3 characters of {_arguments} is "```":
						if first 5 characters of {_arguments} is "```vb": #Soporte de resalto de sintaxis de Visual Basic en bloques de c贸digo
							set {_arguments} to subtext of {_arguments} from character 7 to (length of {_arguments} - 3)
						else:
							set {_arguments} to subtext of {_arguments} from character 4 to (length of {_arguments} - 3)
					else:
						create embed "evaluate"
						set title of embed "evaluate" to ":x: 隆Bloque de c贸digo inv谩lido!"
						set color of embed "evaluate" to "RED"
						send embed "evaluate" to channel event-channel with {@botName}
						delete embed "evaluate"
						stop
				#
				#Eval煤a el c贸digo de Skript y obtiene los errores s铆 hay
				#
				evaluate logging in {_errors::*}: {_arguments}
				if {_errors::*} is set:
					#
					#Cambiar el formato de los errores
					#
					loop {_errors::*}:
						set {_parse::*} to loop-value parsed as "%text% \(TuSKe\/evaluate.sk, line %integer%\: %text%'\)"
						add "**L铆nea %{_parse::2}%:** *%{_parse::1}%*%nl%**>** ``%{_parse::3}%``" to {_lines::*}
					create embed "evaluate"
					set title of embed "evaluate" to ":x: Errores de evaluaci贸n (%size of {_errors::*}%)"
					set description of embed "evaluate" to join {_lines::*} with nl
					set color of embed "evaluate" to "RED"
					send embed "evaluate" to channel event-channel with {@botName}
					delete embed "evaluate"
		else if {_command} is "clear" or "purge":
			if {_arguments} parsed as number is set:
				clear {_arguments} parsed as number messages in event-channel with {@botName}
			else:
				reply with ":exclamation: **Uso del comando: %{_prefix}%**%{_command}% <N煤mero entre 1 y 100>"
