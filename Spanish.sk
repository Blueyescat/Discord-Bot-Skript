# ###############################################################################
# Ejemplo de Skript Discord Bot por Blueyescat
#
# Incluye un buen sistema de evaluaci√≥n.
#
# Requiere los addons de Skript: Vixio y TuSKe
# ###############################################################################

options:
	# Bot Token
	#
	# Obt√©n el token de t√∫ bot en https://discordapp.com/developers
	token: ""
	
	#No le tomes importancia, es el nombre de referencia en el c√≥digo (No es el nombre de usuario en Discord)
	botName: BotName

	# Prefijos de comandos
	#
	# S√≠ quieres usar emojis como prefijo en los comandos, obt√©n el c√°racter unicode de este en: https://emojipedia.org
	# Por ejemplo, :apple: en Discord: "üçé"
	#
	# S√≠ quieres usar la etiqueta de menci√≥n como prefijo de comando, use <@id>
	prefixes: !
	
	#Discord IDs de 18 d√≠gitos de los usuarios administradores
	#
	#Para m√°s de un administrador deber√≠as usar "or" en vez de "and" en la lista.
	admins: "012345678910111213", "312111019876543210" or "981713114610522013"

on load:
	if "{@botName}" parsed as bot is not set: 
		login to {@token} with name "{@botName}"

discord command evaluate <text>:
	aliases: eval
	prefixes: {@prefixes}
	bots: {@botName}
	trigger:
		if id of event-user is {@admins}:
			set {_code} to regex replace "^\s+" with "" in arg-1
			if first 3 characters of {_code} is "```":
				if last 3 characters of {_code} is "```":
					if first 5 characters of {_code} is "```vb": # Syntax colorer VB.Net pour le bloc de code
						set {_code} to subtext of {_code} from character 7 to (length of {_code} - 3)
					else:
						set {_code} to subtext of {_code} from character 4 to (length of {_code} - 3)
				else:
					create embed:
						set description of the embed to ":x: ¬°Bloque de c√≥digo inv√°lido!"
						set color of the embed to color from rgb 255, 0, 0
					reply with the last created embed
					stop
			
			evaluate logging in {_errors::*}: {_code}
			if {_errors::*} is set:
				loop {_errors::*}:
					set {_parse::*} to loop-value parsed as "%text% \(TuSKe\/evaluate.sk, line %integer%\: %text%'\)"
					add "**L√≠nea %{_parse::2}%:** *%{_parse::1}%*%nl%**>** ``%{_parse::3}%``" to {_lines::*}
				create embed:
					set title of the embed to ":x: Errores de evaluaci√≥n (%size of {_errors::*}%)"
					set description of the embed to join {_lines::*} with new line
					set color of the embed to color from rgb 255, 0, 0
				reply with the last created embed
		else:
			reply with ":x: ¬°Los administradores son los √∫nicos que pueden usar este comando!"

discord command purge <text>:
	aliases: purgemessages, clear, clearmessags
	prefixes: {@prefixes}
	executable in: guild
	bots: {@botName}
	trigger:
		if id of event-user is {@admins}:
			set {_count} to arg-1 parsed as number
			if {_count} is greater than 0:
				grab the last {_count} messages in event-channel
				purge the grabbed messages with event-bot
				reply with ":white_check_mark: ¬°Purg√≥ %{_count}% mensaje%(""s"") if {_count} is greater than 1, else """"% con √©xito!" and store the message in {_message}
				wait 2 seconds
				delete random message of {_message} with event-bot
			else:
				reply with ":x: Uso del comando:  **%used prefix%**%used command% **<**un n√∫mero positivo**>**"
		else:
			reply with ":x: ¬°Los administradores son los √∫nicos que pueden usar este comando!"