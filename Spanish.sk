# ###############################################################################
# Ejemplo de Skript Discord Bot por Blueyescat
# Traductor al español: Frank Kusmiruk <https://github.com/FranKusmiruk>
#
# Incluye un buen sistema de evaluación.
#
# Requiere los addons de Skript: Vixio y TuSKe
# ###############################################################################

options:
	# Bot Token
	#
	# Obtén el token de tú bot en https://discordapp.com/developers
	token: "MjE2OTIxNDEyNzgxMjExNjQ4.xXXXxxxXXxXxXXXxXXxxxXxxXXxxXxXXXXxxXxxXXXxxxx"
	
	# No le tomes importancia, es el nombre de referencia en el código (No es el nombre de usuario en Discord) y los dos valores deben ser los mismos
	bot: "BotName"
	botName: BotName

	# Prefijos de comandos
	#
	# Sí quieres usar emojis como prefijo en los comandos, obtén el cáracter unicode de este en: https://emojipedia.org
	# Por ejemplo, :apple: en Discord: "🍎"
	#
	# Sí quieres usar la etiqueta de mención como prefijo de comando, use <@id>
	prefixes: !
	
	#Discord IDs de 18 dígitos de los usuarios administradores
	#
	#Para más de un administrador deberías usar "or" en vez de "and" en la lista.
	admins: "012345678910111213", "312111019876543210" or "981713114610522013"

on load:
	if {@bot} parsed as bot is not set: 
		login to {@token} with name {@bot}
	set status of {@bot} to online

on unload:
	set status of {@bot} to do not disturb

discord command evaluate <text>:
	aliases: eval
	prefixes: {@prefixes}
	bots: {@botName}
	trigger:
		if id of event-user is {@admins}:
			set {_code} to regex replace "^\s+" with "" in arg-1
			if first 3 characters of {_code} is "```":
				if last 3 characters of {_code} is "```":
					if first 5 characters of {_code} is "```vb": # Syntax colorer VB.Net pour le bloc de code
						set {_code} to subtext of {_code} from character 7 to (length of {_code} - 3)
					else:
						set {_code} to subtext of {_code} from character 4 to (length of {_code} - 3)
				else:
					create embed:
						set description of the embed to ":x: ¡Bloque de código inválido!"
						set color of the embed to color from rgb 255, 0, 0
					reply with the last created embed
					stop
			
			evaluate logging in {_errors::*}: {_code}
			if {_errors::*} is set:
				loop {_errors::*}:
					set {_parse::*} to loop-value parsed as "%text% \(TuSKe\/evaluate.sk, line %integer%\: %text%'\)"
					add "**Línea %{_parse::2}%:** *%{_parse::1}%*%nl%**>** ``%{_parse::3}%``" to {_lines::*}
				create embed:
					set title of the embed to ":x: Errores de evaluación (%size of {_errors::*}%)"
					set description of the embed to join {_lines::*} with new line
					set color of the embed to color from rgb 255, 0, 0
				reply with the last created embed
		else:
			reply with ":x: ¡Los administradores son los únicos que pueden usar este comando!"

discord command purge <text>:
	aliases: purgemessages, clear, clearmessages
	prefixes: {@prefixes}
	executable in: guild
	bots: {@botName}
	trigger:
		if id of event-user is {@admins}:
			set {_count} to arg-1 parsed as number
			if {_count} is greater than 0:
				grab the last {_count} messages in event-channel
				purge the grabbed messages with event-bot
				reply with ":white_check_mark: ¡Purgó %{_count}% mensaje%(""s"") if {_count} is greater than 1, else """"% con éxito!" and store the message in {_message}
				wait 2 seconds
				delete random message of {_message} with event-bot
			else:
				reply with ":x: Uso del comando:  **%used prefix%**%used command% **<**un número positivo**>**"
		else:
			reply with ":x: ¡Los administradores son los únicos que pueden usar este comando!"