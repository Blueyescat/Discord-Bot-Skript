# ###############################################################################
# Blueyescat tarafÄ±ndan Skript ile Discord Botu Ã¶rneÄŸi 
#
# GÃ¼zel bir evaluate sistemi iÃ§eriyor.
#
# Vixio ve TuSKe Skript addonlarÄ± gerektirir
# ###############################################################################

options:
	# Bot Tokeni
	#
	# Botunun tokenini buradan alabilirsin: https://discordapp.com/developers
	token: "MjE2OTIxNDEyNzgxMjExNjQ4.xXXXxxxXXxXxXXXxXXxxxXxxXXxxXxXXXXxxXxxXXXxxxx"
	
	# DeÄŸiÅŸtirilmesi Ã¶nemli deÄŸil (Discord'daki kullanÄ±cÄ± adÄ± deÄŸil) ve iki deÄŸer de aynÄ± olmalÄ±
	bot: "BotName"
	botName: BotName
	
	# Komut Ã–nekleri
	#
	# EÄŸer komut Ã¶neklerinde emoji kullanmak istiyorsan, bu emojilerin unicode karakterlerini buradan al: https://emojipedia.org
	# Ã–rnek olarak Discord'da :apple: emojisi: "ğŸ"
	#
	# EÄŸer botunun etiketini kullanmak istiyorsan: <@id>
	prefixes: !
	
	# YÃ¶netici kullanÄ±cÄ±larÄ±n 18 haneli Discord ID'leri
	#
	# Birden fazla yÃ¶netici iÃ§in listede "and" yerine "or" kullanmalÄ±sÄ±n.
	admins: "012345678910111213", "312111019876543210" or "981713114610522013"

on load:
	if {@bot} parsed as bot is not set: 
		login to {@token} with name {@bot}
	set status of {@bot} to online

on unload:
	set status of {@bot} to do not disturb

discord command evaluate <text>:
	aliases: eval
	prefixes: {@prefixes}
	bots: {@botName}
	trigger:
		if id of event-user is {@admins}:
			set {_code} to regex replace "^\s+" with "" in arg-1
			if first 3 characters of {_code} is "```":
				if last 3 characters of {_code} is "```":
					if first 5 characters of {_code} is "```vb": # Kod bloklarÄ±nda VB.Net dili desteÄŸi
						set {_code} to subtext of {_code} from character 7 to (length of {_code} - 3)
					else:
						set {_code} to subtext of {_code} from character 4 to (length of {_code} - 3)
				else:
					create embed:
						set description of the embed to ":x: GeÃ§ersiz kod bloÄŸu!"
						set color of the embed to color from rgb 255, 0, 0
					reply with the last created embed
					stop
			
			evaluate logging in {_errors::*}: {_code}
			if {_errors::*} is set:
				loop {_errors::*}:
					set {_parse::*} to loop-value parsed as "%text% \(TuSKe\/evaluate.sk, line %integer%\: %text%'\)"
					add "**SatÄ±r %{_parse::2}%:** *%{_parse::1}%*%nl%**>** ``%{_parse::3}%``" to {_lines::*}
				create embed:
					set title of the embed to ":x: Evaluate HatalarÄ±(%size of {_errors::*}%)"
					set description of the embed to join {_lines::*} with new line
					set color of the embed to color from rgb 255, 0, 0
				reply with the last created embed
		else:
			reply with ":x: Bu komutu sadece yÃ¶neticiler kullanabilir!"

discord command purge <text>:
	aliases: purgemessages, clear, clearmessages
	prefixes: {@prefixes}
	executable in: guild
	bots: {@botName}
	trigger:
		if id of event-user is {@admins}:
			set {_count} to arg-1 parsed as number
			if {_count} is greater than 0:
				grab the last {_count} messages in event-channel
				purge the grabbed messages with event-bot
				reply with ":white_check_mark: BaÅŸarÄ±yla %{_count}% mesaj temizlendi!" and store the message in {_message}
				wait 2 seconds
				delete random message of {_message} with event-bot
			else:
				reply with ":x: KullanÄ±m:  **%used prefix%**%used command% **<**bir pozitif sayÄ±**>**"
		else:
			reply with ":x: Bu komutu sadece yÃ¶neticiler kullanabilir!"
